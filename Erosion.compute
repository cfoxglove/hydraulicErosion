//
// conventions
//     left = coord.x - 1
//     right = coord.x + 1
//     top = coord.y + 1
//     bottom = coord.y - 1
//

RWTexture2D<float4> PrecipMask;  //defines where rainfall will occur

RWTexture2D<float>  TerrainHeight;      //terrain heightmap
RWTexture2D<float>  TerrainHeightPrev;  //previous frame

RWTexture2D<float>  Water;       //water height
RWTexture2D<float>  WaterPrev;   //water height prev

RWTexture2D<float2> WaterVel;    //water velocity
RWTexture2D<float2> WaterVelPrev; //water vel prev

RWTexture2D<float4> Flux;        //current water flux (how much water is transferring to neighboring cells)
RWTexture2D<float4> FluxPrev;    //flux

RWTexture2D<float>  Sediment;    //suspended sediment concentration (being transported by the water)
RWTexture2D<float>  SedimentPrev;

float dt;          //the time step for the simulation
float precipRate;  //the rate at which we will add water via rainfall
float flowRate;    //how fast water flows
float sedimentCapacity;
float sedimentDissolveRate;
float sedimentDepositRate;
float evaporationRate;

float4 texDim;     //the dimensions of all of our textures (they must all be the same dimensions)

float4 simulateFlux(uint2 coord) {
	float cellSize = 1.0f;
	float g = -9.8f;
	float h = TerrainHeightPrev[coord] + WaterPrev[coord];//WaterPrev[coord];
	
	float fluxCapacitance = (dt * flowRate * g) / cellSize;

	//left
	float4 outFlux;
	float dh = h - (TerrainHeightPrev[uint2(coord.x - 1, coord.y)] + WaterPrev[uint2(coord.x - 1, coord.y)]);
	outFlux.x = max(0.0f, FluxPrev[coord].x + dh * fluxCapacitance);

	//right
	dh = h - (TerrainHeightPrev[uint2(coord.x + 1, coord.y)] + WaterPrev[uint2(coord.x + 1, coord.y)]);
	outFlux.y = max(0.0f, FluxPrev[coord].y + dh * fluxCapacitance);

	//top
	dh = h - (TerrainHeightPrev[uint2(coord.x, coord.y + 1)] + WaterPrev[uint2(coord.x, coord.y + 1)]);
	outFlux.z = max(0.0f, FluxPrev[coord].z + dh * fluxCapacitance);

	//bottom
	dh = h - (TerrainHeightPrev[uint2(coord.x, coord.y - 1)] + WaterPrev[uint2(coord.x, coord.y - 1)]);
	outFlux.w = max(0.0f, FluxPrev[coord].w + dh * fluxCapacitance);

	return outFlux;
}

float2 simulateWaterVelocity(uint2 coord) {
	float inFlow = Flux[uint2(coord.x - 1, coord.y)].y +
				   Flux[uint2(coord.x + 1, coord.y)].x +
				   Flux[uint2(coord.x, coord.y - 1)].z +
				   Flux[uint2(coord.x, coord.y + 1)].w;

	float outFlow = Flux[coord].x + Flux[coord].y + Flux[coord].z + Flux[coord].w;

	float dx = 1.0f; //grid dims?
	float dy = 1.0f; 

	//??? why are we updating water here???
	float dV = dt * (inFlow - outFlow);
	float oldWater = Water[coord];
	Water[coord] += dV / (dx * dy);
	Water[coord] = max(0.0f, Water[coord]);

	//update velocities
	float meanWater = 0.5f * (oldWater + Water[coord]);
	float2 newWaterVel;
	if (meanWater == 0.0f) {
		newWaterVel = float2(0.0f, 0.0f);
	}
	else {
		//I'm not confident on this line - should this be 1/meanWater or meanWater???
		newWaterVel.x = meanWater * 0.5f * (Flux[uint2(coord.x - 1, coord.y)].y - Flux[coord].x - Flux[uint2(coord.x + 1, coord.y)].x + Flux[coord].y) / (dy);// *meanWater);
		newWaterVel.y = meanWater * 0.5f * (Flux[uint2(coord.x, coord.y - 1)].z - Flux[coord].w - Flux[uint2(coord.x, coord.y + 1)].w + Flux[coord].z) / (dx);// *meanWater);
	}

	return newWaterVel;
}

float hash(float n)
{
	return frac(sin(n)*43758.5453f);
}

float noise(float3 x)
{
	float3 p = floor(x);
	float3 f = frac(x);

	f = f * f * (3.0 - 2.0 * f);

	float n = p.x + p.y * 57.0 + 113.0 * p.z;

	float res = lerp(lerp(lerp(hash(n + 0.0), hash(n + 1.0), f.x),
		lerp(hash(n + 57.0), hash(n + 58.0), f.x), f.y),
		lerp(lerp(hash(n + 113.0), hash(n + 114.0), f.x),
			lerp(hash(n + 170.0), hash(n + 171.0), f.x), f.y), f.z);
	return res;
}

float fbm(float3 p)
{
	float f;
	f = 0.5000*noise(p); p = p * 2.02;
	f += 0.2500*noise(p); p = p * 2.03;
	f += 0.1250*noise(p);
	return f;
}

float rainPosition;
void simulateWater(uint2 coord) {
	//rainfall
	float rainScale = 0.06f;
	float3 rainPos = float3(rainScale * (float)coord.x, rainScale * (float)coord.y, rainPosition);
	float rainfall = precipRate * dt * fbm(rainPos);// *PrecipMask[coord].r;

	float evaporated = evaporationRate * dt;

	Flux[coord] = simulateFlux(coord);
	WaterVel[coord] = simulateWaterVelocity(coord);

	Water[coord] = max(WaterPrev[coord] + rainfall - evaporated, 0.0f);
}

#pragma kernel SimulateWaterFlow

[numthreads(8,8,1)]
void SimulateWaterFlow(uint3 id : SV_DispatchThreadID)
{
	simulateWater(id.xy);
}


//
// Hydraulic Erosion
//
void simulateErosion(uint2 coord) {
	float kc = sedimentCapacity;        // sediment capacity
	float ks = sedimentDissolveRate;    // dissolving constant
	float kd = sedimentDepositRate;     // deposition constant

	float waterLevel = Water[coord];    //use Water, not WaterPrev here because we have just simulated water flow
	float sediment = SedimentPrev[coord];
	float terrainHeight = TerrainHeightPrev[coord];

	uint ridx = (coord.x < (uint)texDim[0]) ? coord.x + 1 : coord.x;
	uint lidx = (coord.x > 0) ? coord.x - 1 : coord.x;
	uint tidx = (coord.y < (uint)texDim[1]) ? coord.y + 1 : coord.y;
	uint bidx = (coord.y < 0) ? coord.y - 1 : coord.y;

	float3 n = float3(TerrainHeightPrev[uint2(ridx, coord.y)] - TerrainHeightPrev[uint2(lidx, coord.y)],
					  TerrainHeightPrev[uint2(coord.x, tidx)] - TerrainHeightPrev[uint2(coord.x, bidx)],
					  2.0f);
	n = normalize(n);
	float cosa = dot(n, float3(0.0f, 0.0f, 1.0f));
	float sinAlpha = max(0.1f, sin(acos(cosa)));

	//use watervel here because we have just computed it for this frame in the previous stage
	float flowSpeed = sqrt(WaterVel[coord].x * WaterVel[coord].x + WaterVel[coord].y * WaterVel[coord].y);

	//local sediment capacity of the flow
	float cap = kc * flowSpeed * sinAlpha * (min(Water[coord], 0.01f) / 0.01f);
	float delta = (cap - SedimentPrev[coord]);

	float fctr = min(Water[coord], 0.01f) / 0.01f;

	if (delta > 0.0f) {
		//pick up sediment
		float d = ks * delta;
		TerrainHeight[coord] = saturate(terrainHeight - d);
		Water[coord] = saturate(waterLevel + d);
		Sediment[coord] = saturate(sediment + d);
	}
	else
	{
		//deposit sediment
		float d = kd * delta;
		TerrainHeight[coord] = terrainHeight + d;
		Water[coord] = saturate(waterLevel - d);
		Sediment[coord] = saturate(sediment - d);
	}
}

void simulateSedimentTransport(uint2 coord) {
	//use WaterVel not WaterVelPrev because we calculated it in the previous stage
	float fromX = (float)coord.x - WaterVel[coord].x;
	float fromY = (float)coord.y - WaterVel[coord].y;

	uint2 cell0 = uint2((uint)fromX, (uint)fromY);
	uint2 cell1 = cell0 + uint2(1, 1);

	float fx = fromX - (float)cell0.x;
	float fy = fromY - (float)cell0.y;

	Sediment[coord] = lerp(lerp(SedimentPrev[cell0], SedimentPrev[uint2(cell1.x, cell0.y)], fx), lerp(SedimentPrev[uint2(cell0.x, cell1.y)], SedimentPrev[cell1], fx), fy);
}

#pragma kernel HydraulicErosion

[numthreads(8,8,1)]
void HydraulicErosion (uint3 id : SV_DispatchThreadID)
{	
	simulateErosion(id.xy);
	simulateSedimentTransport(id.xy);
}


//
// Thermal Erosion
//
float angleOfRepose; //actually tan(theta)
int numThermalIterations;

void simulateThermalErosion(uint2 coord) {
	if (coord.x <= 0 || coord.x > (uint)texDim[0] || coord.y <= 0 || coord.y > (uint)texDim[1]) {
		return;
	}

	float h = TerrainHeightPrev[coord];
	float dx = 1.0f / texDim[0];

	float a = 1.0f / (texDim[0] * texDim[1]);

	float dhl = h - TerrainHeightPrev[uint2(coord.x - 1, coord.y)];
	float dhr = h - TerrainHeightPrev[uint2(coord.x + 1, coord.y)];
	float dht = h - TerrainHeightPrev[uint2(coord.x, coord.y + 1)];
	float dhb = h - TerrainHeightPrev[uint2(coord.x, coord.y - 1)];

	if (abs(dhl / dx) > angleOfRepose) {
		TerrainHeight[coord] -= a * dt * dhl / 8.0f;
	}
	if (abs(dhr / dx) > angleOfRepose) {
		TerrainHeight[coord] -= a * dt * dhr / 8.0f;
	}
	if (abs(dht / dx) > angleOfRepose) {
		TerrainHeight[coord] -= a * dt * dht / 8.0f;
	}
	if (abs(dhb / dx) > angleOfRepose) {
		TerrainHeight[coord] -= a * dt * dhb / 8.0f;
	}
}

#pragma kernel ThermalErosion

[numthreads(8, 8, 1)]
void ThermalErosion(uint3 id : SV_DispatchThreadID)
{
	//for (int i = 0; i < numThermalIterations; i++) {
		simulateThermalErosion(id.xy);
	//}
}